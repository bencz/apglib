/*‚*-----------------------------------------------------------------*/
/*‚* ALCOBJ                                                          */
/*‚*---------------------------------------------------------------  */
/*‚*                                                                 */
/*‚* AUTOR         :  A. PIEGER                                      */
/*‚*                                                                 */
/*‚* ERSTELLT AM   :  08.01.2010                                     */
/*‚*                                                                 */
/*‚* FUNKTION      :  LOCK AUF DATEI SETZEN                          */
/*‚*                                                                 */
/*‚* ÄNDERUNGEN:                                                     */
/*‚*‚DATUM      VON   GRUND DER ÄNDERUNG                             */
/*‚*                                                                 */
/*‚****************************************************************  */
             PGM        PARM(&ALCOBJ_REC &RETCODE)

             DCL        VAR(&ALCOBJ_REC)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&RETCODE)     TYPE(*CHAR) LEN(1)

             DCL        VAR(&LIBNAME)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILENAME)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBRNAME)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYPNAME)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&SPRSTS)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&WAIT)        TYPE(*DEC)  LEN(3)
/*‚          VARIABLEN AUS RECORD ÜBERNEHMEN                         */
             CHGVAR     VAR(&LIBNAME)    VALUE(%SST(&ALCOBJ_REC 01 10))
             CHGVAR     VAR(&FILENAME)   VALUE(%SST(&ALCOBJ_REC 11 10))
             CHGVAR     VAR(&MBRNAME)    VALUE(%SST(&ALCOBJ_REC 21 10))
             CHGVAR     VAR(&TYPNAME)    VALUE(%SST(&ALCOBJ_REC 31 10))
             CHGVAR     VAR(&SPRSTS)     VALUE(%SST(&ALCOBJ_REC 41 7))
             CHGVAR     VAR(&WAIT)       VALUE(%SST(&ALCOBJ_REC 48 3))
             MONMSG     MSGID(CPF0000) EXEC(DO)
                        CHGVAR VAR(&WAIT) VALUE(000)
             ENDDO

/*‚          RET-CODE ZURÜCKSETZEN                                   */
             CHGVAR     VAR(&RETCODE)  VALUE(' ')

/*‚          WENN BIBLIOTHEK NICHT GEFÜLLT, DANN *LIBL ANNEHMEN      */
             IF         COND(&LIBNAME *EQ ' ') THEN(DO)
                        CHGVAR VAR(&LIBNAME) VALUE('*LIBL')
             ENDDO

/*‚          WENN SPERRSTATUS NICHT GEFÜLLT, DEFAULT NEHMEN          */
             IF         COND(&SPRSTS *EQ ' ') THEN(DO)
                        CHGVAR VAR(&SPRSTS) VALUE('*EXCL')
             ENDDO

/*‚          WENN ZEIT NICHT GEFÜLLT, DEFAULT NEHMEN                 */
             IF         COND(&WAIT *EQ 000) THEN(DO)
                        CHGVAR VAR(&WAIT) VALUE(000)
             ENDDO

/*‚          WENN MBR NICHT GEFÜLLT, DEFAULT NEHMEN                  */
             IF         COND(&MBRNAME *EQ ' ') THEN(DO)
                        CHGVAR VAR(&MBRNAME) VALUE('*FIRST')
             ENDDO

             ALCOBJ     OBJ((&LIBNAME/&FILENAME &TYPNAME        +
                             &SPRSTS &MBRNAME)) WAIT(&WAIT)
             MONMSG     MSGID(CPF0000) EXEC(DO)
                        CHGVAR VAR(&RETCODE) VALUE('1')
                        GOTO ENDPGM
             ENDDO

 ENDPGM:     ENDPGM
