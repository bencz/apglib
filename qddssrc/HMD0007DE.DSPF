     A*%%TS  SD  20110726  071142  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* WINDOW ZUR TAG-SELEKTION                                      *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  02.02.2009                                   *
     A*                                                               *
     A* FUNKTION      :  WINDOW ZUR TAG-SELEKTION                     *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A  *DS4                                MSGLOC(27)
     A  *DS3                                MSGLOC(24)
     A                                      PRINT
     A                                      CA05(05)
     A                                      CA12(12)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20110726  071142  APG         REL-V5R4M0  5722-WDS
     A            PTLFDN    R        H      REFFLD(PTLFDN APG/PTAGLIB)
     A            AUSW           1A  B  4  2VALUES(' ' '1' '4')
     A            PTTAG     R        O  4  4REFFLD(PTTAG APG/PTAGLIB)
     A  42                                  DSPATR(HI)
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20090202  230453  APG         REL-V5R4M0  5722-WDS
     A                                      CA13(13)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(FMT05)
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            PTTAG     R        B  3  4REFFLD(PTTAG APG/PTAGLIB)
     A                                      DSPATR(PC)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT04
     A*%%TS  SD  20090202  230050  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      WINDOW(FMT05)
     A*--------------------------------------------------------------
     A          R FMT05
     A*%%TS  SD  20090202  230453  APG         REL-V5R4M0  5722-WDS
     A                                      WINDOW(12 55 10 19)
     A                                  9  1'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  19940426  153744  UB          REL-V2R2M0  5738-PW1
     A                                      ASSUME
     A                                 01 02' '
     A                                      DSPATR(ND)
