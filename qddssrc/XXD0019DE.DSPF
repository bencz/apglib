     A*%%TS  SD  20121128  071511  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* DETAILANZEIGE OPL                                             *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  15.03.2011                                   *
     A*                                                               *
     A* FUNKTION      :  DETAILANZEIGE OPL-SATZES ANHAND LFDN         *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20121128  071511  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A            OPLFDN    R        H      REFFLD(OPLFDN APG/POPLLST)
     A            OPSHRM    R        H      REFFLD(OPSHRM APG/POPLLST)
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            A_OPUSER       1A  P
     A            AUSW           1A  B 23  2VALUES(' ' '2' '4' '5')
     A            OPUSER    R        O 23  4REFFLD(OPUSER APG/POPLLST)
     A  02                                  DSPATR(&A_OPUSER)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20121128  071511  APG         REL-V5R4M0  5722-WDS
     A  *DS4                                SFLLIN(0003)
     A                                      SFLSIZ(0022)
     A                                      SFLPAG(0013)
     A                                      CA05(05)
     A                                      CF08(08)
     A                                      CA09(09)
     A                                      CA12(12)
     A                                      CA20(20)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A  41                              4115'Neuanlage'
     A                                      COLOR(RED)
     A                                  5  2'Laufende Nr.  . :'
     A            OPLFDN    R        O  5 20REFFLD(OPLFDN APG/POPLLST)
     A                                      EDTCDE(3)
     A                                      DSPATR(HI)
     A                                  5 93'Anlage  . . . . :'
     A                                  6 95'User  . . . . :'
     A            OPAUSR    R        O  6111REFFLD(OPAUSR APG/POPLLST)
     A            OPABS     R        O  6122REFFLD(OPABS APG/POPLLST)
     A                                  7  2'Datum . . . . . :'
     A            DT_O          10A  O  7 20
     A            DF_O          10A  O  7 31COLOR(BLU)
     A                                  7 95'Datum/Zeit  . :'
     A            DT_A          10A  O  7111
     A            TI_A           8A  O  7122
     A                                  8  2'Kategorie . . . :'
     A            OPTYPE    R        O  8 20REFFLD(OPTYPE APG/POPLLST)
     A            TYPTXT        25A  O  8 42COLOR(BLU)
     A                                  9  2'User  . . . . . :'
     A            OPUSER    R        O  9 20REFFLD(OPUSER APG/POPLLST)
     A                                  9 93'Modifikation  . :'
     A                                 10 95'User  . . . . :'
     A            OPMSER    R        O 10111REFFLD(OPMSER APG/POPLLST)
     A            OPMBS     R        O 10122REFFLD(OPMBS APG/POPLLST)
     A                                 11  2'Sharenumber . . :'
     A            OPSHRN    R        O 11 20REFFLD(OPSHRN APG/POPLLST)
     A                                      EDTCDE(3)
     A                                 11 95'Datum/Zeit  . :'
     A            DT_M          10A  O 11111
     A            TI_M           8A  O 11122
     A                                 12  4'Sharemanager  :'
     A            OPSHRM    R        O 12 20REFFLD(OPSHRM APG/POPLLST)
     A                                 14  2'Text  . . . . . :'
     A            OPTEXT    R   80   B 14 20REFFLD(OPTEXT APG/POPLLST)
     A                                      CHECK(LC)
     A                                 15  4'Zusätz. Text  :'
     A            TXTVOR         1Y 0O 15 20EDTCDE(3)
     A                                 15 23'(0=nein/1=ja)'
     A                                      COLOR(BLU)
     A                                 16  2'Status  . . . . :'
     A            OPPRZT    R        B 16 20REFFLD(OPPRZT APG/POPLLST)
     A                                      EDTCDE(4)
     A                                 16 24'%'
     A                                      COLOR(BLU)
     A                                 17  2'Priorität . . . :'
     A            OPPRIO    R     Y  B 17 20REFFLD(OPPRIO APG/POPLLST)
     A                                      EDTCDE(4)
     A                                 19  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                 20  4'4=Löschen'
     A                                      COLOR(BLU)
     A            SFT0009A      10A  O 22  4DSPATR(HI)
     A            SFT0009B      10A  O 22 19DSPATR(HI)
     A            SFT0009C      10A  O 22 34DSPATR(HI)
     A            SFT0009D      10A  O 22 49DSPATR(HI)
     A            SFT0009E      10A  O 22 64DSPATR(HI)
     A            SFT0009F      10A  O 22 79DSPATR(HI)
     A            SFT0009G      10A  O 22 94DSPATR(HI)
     A            SFT0009H      10A  O 22109DSPATR(HI)
     A*            17:14:43    QPGMR       REL-R07M00  5714-UT1
     A*                                     OVERLAY
     A          R FMT04
     A*%%TS  SD  20110316  065736  APG         REL-V5R4M0  5722-WDS
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F8=Neuer User'
     A                                      COLOR(BLU)
     A                                 26 49'F9=Text pflegen'
     A                                      COLOR(BLU)
     A                                 26 66'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  20110316  065736  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 24 39'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
