     A*%%TS  SD  20100126  200932  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* ANZEIGE TEMPLATEPLATZHALTER                                   *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  26.01.2010                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE DER PLATZHALTER                      *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A  *DS3                                MSGLOC(24)
     A  *DS4                                MSGLOC(27)
     A                                      PRINT
     A                                      CA05(05)
     A                                      CA12(12)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20100126  200725  APG         REL-V5R4M0  5722-WDS
     A            LFDN           4S 0H
     A            TMPCMD        20A  O  4  4
     A            TMPVAL        30A  B  4 25
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20100126  200932  APG         REL-V5R4M0  5722-WDS
     A  *DS3                                SFLSIZ(0007)
     A  *DS4                                SFLSIZ(0007)
     A  *DS3                                SFLPAG(0006)
     A  *DS4                                SFLPAG(0006)
     A  *DS3                                WINDOW(FMT05)
     A  *DS4                                WINDOW(FMT05)
     A  03                                  CSRLOC(CURY       CURX)
     A                                      CF02(02)
     A                                      BLINK
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A                                  1  2'CRD0019DE'
     A                                      COLOR(BLU)
     A            MAINUEB       21A  O  1 13DSPATR(HI)
     A            SFT0022       20A  O  3  4DSPATR(HI)
     A            SFT0023       30A  O  3 25DSPATR(HI)
     A*--------------------------------------------------------------
     A          R FMT04
     A*%%TS  SD  20090127  224738  APG         REL-V5R4M0  5722-WDS
     A                                      WINDOW(FMT05)
     A                                      OVERLAY
     A                                  7  2'Keine Daten angelegt'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------
     A          R FMT05
     A*%%TS  SD  20100126  195937  APG         REL-V5R4M0  5722-WDS
     A  *DS3                                WINDOW(8 3 11 54)
     A  *DS4                                WINDOW(8 3 11 54)
     A                                 10  1'F2=Starten'
     A                                      COLOR(BLU)
     A                                 10 13'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  19940426  153744  UB          REL-V2R2M0  5738-PW1
     A                                      ASSUME
     A                                 01 02' '
     A                                      DSPATR(ND)
