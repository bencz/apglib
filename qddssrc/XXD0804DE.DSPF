     A*%%TS  SD  20100924  071827  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* PROJEKTSOURCEN                                                *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  14.09.2010                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE PROJEKTSOURCEN                       *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      ERRSFL
     A                                      PRINT
     A                                      HELP
     A                                      CA03(03 'Ende/Zurück')
     A*---------------------------------------------------------------*
     A          R FMT02                     SFL
     A*%%TS  SD  20100924  071611  APG         REL-V5R4M0  5722-WDS
     A                                      SFLNXTCHG
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            A_AUSW         1A  P
     A            AUSW           2A  B 10  2
     A  02                                  DSPATR(&A_AUSW)
     A            SRCNAM    R        O 10  5REFFLD(COLMBR APG/PPRJMBR)
     A            COSRCN    R        O 10 26REFFLD(COSRCN APG/PPRJMBR)
     A            COSLIB    R        O 10 37REFFLD(COSLIB APG/PPRJMBR)
     A            COTYP     R        O 10 48REFFLD(COTYP APG/PPRJMBR)
     A            COLANG    R        O 10 59REFFLD(COLANG PPRJMBR)
     A                                      COLOR(PNK)
     A            DT_SRC        10A  O 10 63
     A            COTEXT    R        O 10 74REFFLD(COTEXT APG/PPRJMBR)
     A                                 11  5'Projekt:'
     A                                      COLOR(BLU)
     A            COPROJ    R        O 11 14REFFLD(COPROJ APG/PPRJMBR)
     A                                      COLOR(BLU)
     A                                 11 25'Teilprojekt:'
     A                                      COLOR(BLU)
     A            COTPRJ    R        O 11 38REFFLD(COTPRJ APG/PPRJMBR)
     A                                      COLOR(BLU)
     A                                 11 49'Version:'
     A                                      COLOR(BLU)
     A            FLD003         9A  O 11 58COLOR(BLU)
     A*€75                                  COLOR(RED)
     A*€N75                                 COLOR(BLU)
     A*---------------------------------------------------------------*
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20100924  071827  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      CF04(04 'PROMPT')
     A                                      CA05(05 'AKTUALISIEREN')
     A                                      CA09(09 'AUFFINDEN')
     A                                      CA10(10 'BEFEHLSEINGABE')
     A                                      CA12(12 'ZURÜCK')
     A                                      CF13(13 'WIEDERHOLEN')
     A                                      CA14(14 'SORT')
     A                                      CA16(16 'KOMMANDO DATENBANK ÖFFNEN')
     A                                      CA19(19 'NUR GEÄNDERTE ANZ')
     A                                      CA20(20 'NORMALE TEILDATEIENANSICHT-
     A                                      ')
     A                                      CA23(23 'WEITERE ANGABEN')
     A                                      CA21(21 'Bedeutung Farbcodes')
     A                                      CA08(08 'Hinzufügen')
     A                                      KEEP
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A                                      SFLMODE(&SFLMODE)
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      SFLDROP(CA01)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG       139   M
     A*
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            SFLMODE        1A  H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       93A  O  1 21DSPATR(HI)
     A            PGMTYP         8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        93A  O  2 21DSPATR(HI)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A            ANZREC         4Y 0O  3  2EDTCDE(4)
     A                                      COLOR(RED)
     A                                  3  7'/'
     A                                      COLOR(RED)
     A            MAXMBR         6Y 0O  3  9EDTCDE(4)
     A                                      COLOR(RED)
     A                                  4  2'Bibliothek :'
     A            UPMLIB    R     A  B  4 15REFFLD(COSLIB APG/PPRJMBR)
     A  76                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                  4 26'Datei  . . :'
     A            UPMFIL    R     A  B  4 39REFFLD(COSRCN APG/PPRJMBR)
     A                                      DSPATR(HI)
     A*€76                                  DSPATR(PR)
     A                                  4 50'Teildatei  :'
     A            UPMMBR    R     A  B  4 63REFFLD(COLMBR APG/PPRJMBR)
     A                                      DSPATR(HI)
     A                                  4 84'Listenanf. :'
     A            PLMBR     R     A  B  4 97REFFLD(COLMBR APG/PPRJMBR)
     A                                      DSPATR(HI)
     A  42                              5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A N81        AUSWZ1A      129A  O  6  4COLOR(BLU)
     A  81        AUSWZ1B      129A  O  6  4COLOR(BLU)
     A N81        AUSWZ2A      129A  O  7  4COLOR(BLU)
     A  81        AUSWZ2B      129A  O  7  4COLOR(BLU)
     A            SFT0487       20A  O  9  5DSPATR(HI)
     A            SFT0144       10A  O  9 26DSPATR(HI)
     A            SFT0674       10A  O  9 37DSPATR(HI)
     A            SFT0583       10A  O  9 48DSPATR(HI)
     A            SFT0057        3A  O  9 59DSPATR(HI)
     A            SFT0338       10A  O  9 63DSPATR(HI)
     A            SFT0302       50A  O  9 74DSPATR(HI)
     A*---------------------------------------------------------------*
     A*            17:14:43    QPGMR       REL-R07M00  5714-UT1
     A          R FMT04
     A*%%TS  SD  20100326  083741  DSM         REL-V6R1M0  5761-WDS
     A                                      CF04(04 'PROMPT')
     A                                      CA05(05 'aktualisieren')
     A                                      CA09(09 'AUFFINDEN')
     A                                      CA10(10 'Befehlseingabe')
     A                                      CA12(12 'Zurück')
     A                                      CF13(13 'WIEDERHOLEN')
     A                                      CA14(14 'SORT')
     A                                      CA16(16 'KOMMANDO DATENBANK ÖFFNEN')
     A                                      CA19(19 'NUR GEÄNDERTE ANZ')
     A                                      CA21(21 'Bedeutung Farbcodes')
     A                                      BLINK
     A                                      OVERLAY
     A N77                             24  2'===>'
     A N77        DEMCMD       125A  B 24  7CHECK(LC)
     A  52                                  DSPATR(PC)
     A                                 25  2'F1=2.Zeile'
     A                                      COLOR(BLU)
     A                                 25 14'F3=Verl.'
     A                                      COLOR(BLU)
     A                                 25 24'F4=Bedienerfüh.'
     A                                      COLOR(BLU)
     A                                 25 41'F5=Akt.'
     A                                      COLOR(BLU)
     A  43                             25 50'F8=Hinzufügen'
     A                                      COLOR(BLU)
     A                                 25 65'F9=Auffinden'
     A                                      COLOR(BLU)
     A                                 25 79'F10=Befehlseingabe'
     A                                      COLOR(BLU)
     A                                 25 99'F12=Abbr.'
     A                                      COLOR(BLU)
     A                                 25110'F13=Wiederholen'
     A                                      COLOR(BLU)
     A N50                             26  2'F14=Sortieren'
     A                                      COLOR(BLU)
     A N50                             26 17'F16=Benutzerang.'
     A                                      COLOR(BLU)
     A  51                             26 35'F19=Anz. Alle'
     A                                      COLOR(BLU)
     A  41                             26 51'F20=Teildateienansicht'
     A                                      COLOR(BLU)
     A N51                             26 52'F19=Anz.Modif.'
     A                                      COLOR(BLU)
     A                                 26 75'F21=Bedeutung Farbcodes'
     A                                      COLOR(BLU)
     A  53                             26100'F23=Weitere Angaben'
     A                                      COLOR(BLU)
     A*---------------------------------------------------------------*
     A          R FMT05
     A*%%TS  SD  20080307  100604  AMU         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 15 41'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden'
     A                                      DSPATR(HI)
