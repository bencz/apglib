     A*%%TS  SD  20100817  130111  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* ANZEIGE ALLER INFOTEXTE                                       *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  17.08.2010                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE ALLER INFOTEXTE                      *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20100817  130111  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A            HWUSER    R        H      REFFLD(HWUSER APG/PHINTXT)
     A            AUSW           1A  B  8  2VALUES(' ' '1' '4' '8')
     A            HWPGM     R        O  8  4REFFLD(HWPGM APG/PHINTXT)
     A            HWLFPO    R        O  8 15REFFLD(HWLFPO APG/PHINTXT)
     A                                      EDTCDE(3)
     A            HWTEXT    R   55   O  8 25REFFLD(HWTEXT APG/PHINTXT)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20100817  130111  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CA05(05)
     A                                      CA08(08)
     A                                      CA12(12)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            AULFDN    R        H      REFFLD(AULFDN APG/PAZUUEB)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       38A  O  1 20DSPATR(HI)
     A            PGMTYP         8A  O  1 62COLOR(BLU)
     A                                  1 72SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        38A  O  2 20DSPATR(HI)
     A            DT_SYS         8A  O  2 62
     A            TI_SYS         8A  O  2 72
     A                                  4  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  5  4'1=Für alle als gelesen markieren'
     A                                      COLOR(BLU)
     A            SFT0002       10A  O  7  4DSPATR(HI)
     A            SFT0038        9   O  7 15DSPATR(HI)
     A            SFT0006       55A  O  7 25DSPATR(HI)
     A          R FMT04
     A*%%TS  SD  20100803  173201  APG         REL-V5R4M0  5722-WDS
     A                                 23  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 23 34'F8=Neuanlage'
     A                                      COLOR(BLU)
     A                                 23 48'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  20090127  183236  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 15 13'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
