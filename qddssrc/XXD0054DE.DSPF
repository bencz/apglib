     A*%%TS  SD  20130602  141120  APG         REL-V6R1M0  5761-WDS
     A*---------------------------------------------------------------*
     A* ANZEIGE COBOL-CODE ZUM START/READ-NEXT                        *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  30.05.2013                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE PROGRAMMCODE                         *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A  *DS4                                MSGLOC(27)
     A  *DS3                                MSGLOC(24)
     A                                      PRINT
     A                                      CA05(05)
     A                                      CA12(12)
     A                                      CA20(20)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20130530  091755  APG         REL-V6R1M0  5761-WDS
     A  04                                  SFLNXTCHG
     A            A_COBCDE       1A  P
     A            COBCDE        72A  O  3  1
     A  02                                  DSPATR(&A_COBCDE)
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20130530  091755  APG         REL-V6R1M0  5761-WDS
     A  *DS4                                SFLSIZ(0013)
     A  *DS3                                SFLSIZ(0013)
     A  *DS4                                SFLPAG(0012)
     A  *DS3                                SFLPAG(0012)
     A  *DS4                                WINDOW(FMT04)
     A  *DS3                                WINDOW(FMT04)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SYS_DSP       10A  O  1  1COLOR(BLU)
     A            SYS_FMT       10A  O  1 12COLOR(BLU)
     A            MAINUEB       50A  O  1 23DSPATR(HI)
     A*--------------------------------------------------------------
     A          R FMT04
     A*%%TS  SD  20130602  141120  APG         REL-V6R1M0  5761-WDS
     A  *DS4                                WINDOW(7 50 16 74)
     A  *DS3                                WINDOW(7 2 16 74)
     A                                      OVERLAY
     A                                 15  1'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 15 19'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 15 34'F20=Ansicht wechseln'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT05
     A*%%TS  SD  20110131  071559  APG         REL-V5R4M0  5722-WDS
     A  *DS3                                WINDOW(FMT04)
     A  *DS4                                WINDOW(FMT04)
     A                                      OVERLAY
     A                                 10 14'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  19940426  153744  UB          REL-V2R2M0  5738-PW1
     A                                      ASSUME
     A                                 01 02' '
     A                                      DSPATR(ND)
