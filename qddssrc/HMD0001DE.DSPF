     A*%%TS  SD  20111117  155106  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* HERMES                                                        *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  08.01.2009                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE UND AUSWAHL ALLER PROGRAMME          *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A          R FMT01
     A*%%TS  SD  20110701  072950  APG         REL-V5R4M0  5722-WDS
     A                                      CA12(12)
     A                                      CA18(18)
     A                                      CA20(20)
     A                                      CA21(21)
     A  03                                  CSRLOC(CURY       CURX)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A*
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            A_PLLIB        1A  P
     A            A_TAG1         1A  P
     A            A_TAG2         1A  P
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                 18  2'Tags . . . . . . .?:'
     A            TAG1          10A  B 18 23
     A  02                                  DSPATR(&A_TAG1)
     A            TAG2          10A  B 18 34
     A  02                                  DSPATR(&A_TAG2)
     A            TAG3          10A  B 18 45
     A            TAG4          10A  B 18 56
     A            TAG5          10A  B 18 67
     A            WEITERE        1A  B 18 78VALUES(' ' '+')
     A                                 20  2'Datei  . . . . . . :'
     A            PLFILE    R        B 20 23REFFLD(PLFILE APG/PPGMLIB)
     A                                 20 35'(mit Funktion *)'
     A                                      COLOR(BLU)
     A                                 21  2'Bibliothek . . . . :'
     A            PLLIB     R        B 21 23REFFLD(PLLIB APG/PPGMLIB)
     A  02                                  DSPATR(&A_PLLIB)
     A                                 21 35'(z.B. LFS)'
     A                                      COLOR(BLU)
     A                                 22  2'Dateiart . . . . . :'
     A            PLSEU2    R        B 22 23REFFLD(PLSEU2 APG/PPGMLIB)
     A                                 22 35'(PF/LF/CBLLE/...)'
     A                                      COLOR(BLU)
     A                                 24  2'Beschreibung . . . :'
     A            TXTSEL        30A  B 24 23
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 26 49'F18=Tags pflegen'
     A                                      COLOR(BLU)
     A                                 26 67'F20=Infos'
     A  40                                  COLOR(RED)
     A N40                                  COLOR(BLU)
     A  40                                  DSPATR(BL)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20111117  155106  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            PLSEU2    R        H      REFFLD(PLSEU2 APG/PPGMLIB)
     A            AUSW           1A  B 10  2VALUES(' ' '1' '4' '5' '6')
     A            PLLIB     R        O 10  4REFFLD(PLLIB APG/PPGMLIB)
     A            PLFILE    R        O 10 15REFFLD(PLFILE APG/PPGMLIB)
     A            PLMTXT    R        O 10 26REFFLD(PLMTXT APG/PPGMLIB)
     A            PLKOM1    R   55   O 10 77REFFLD(PLKOM1 APG/PPGMLIB)
     A                                      COLOR(BLU)
     A            PLKOM2    R   55   O 11  4REFFLD(PLKOM2 APG/PPGMLIB)
     A                                      COLOR(BLU)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20090520  174743  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      CA05(05)
     A                                      CA08(08)
     A                                      CA12(12)
     A                                      CA13(13)
     A                                      CA21(21)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  6  4'1=Auswählen'
     A                                      COLOR(BLU)
     A                                  6 17'4=Löschen'
     A                                      COLOR(BLU)
     A                                  6 28'5=Source anzeigen'
     A                                      COLOR(BLU)
     A                                  6 47'6=Beschreibung'
     A                                      COLOR(BLU)
     A            SFT0001       10A  O  9  4DSPATR(HI)
     A            SFT0002       10A  O  9 15DSPATR(HI)
     A            SFT0003       50A  O  9 26DSPATR(HI)
     A          R FMT04
     A*%%TS  SD  20110701  072856  APG         REL-V5R4M0  5722-WDS
     A*                                     OVERLAY
     A                                 26  2'F1=Zweite Zeile'
     A                                      COLOR(BLU)
     A                                 26 19'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 33'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 51'F8=Neuanlage'
     A                                      COLOR(BLU)
     A                                 26 65'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 26 80'F13=Weitere Selektionen'
     A                                      COLOR(BLU)
     A                                 26105'F21=Kommandozeile'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  19931217  173127  HEH         REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                 15 39'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
