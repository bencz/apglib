     A*%%TS  SD  20090604  214536  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* ANZEIGE EINES THEMAS                                          *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  04.06.2009                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE EINES THEMAS                         *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20090604  210736  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            AUSW           1A  B 16  2VALUES(' ')
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A            PSTEXT    R   76   O 16  4REFFLD(PSTEXT APG/PAJXTXT)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20090604  214536  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      CA05(05)
     A                                      CA09(09)
     A                                      CA12(12)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A                                      TEXT('REL.-RECORD-NR FEHLERH.-SATZ')
     A                                  1  2'AJD0002DE'
     A                                      COLOR(BLU)
     A                                  1 13'FMT03'
     A                                      COLOR(BLU)
     A                                  1 13'FMT03'
     A                                      COLOR(BLU)
     A            MAINUEB       38A  O  1 20DSPATR(HI)
     A            PGMTYP         8A  O  1 62COLOR(BLU)
     A                                  1 72SYSNAME
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A            LANGID         3A  O  2 13MSGID(SP0 0001 QTEMP/APGSPRACH)
     A                                      COLOR(BLU)
     A            SUBUEB        38A  O  2 20DSPATR(HI)
     A                                  2 62DATE(*SYS *Y)
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  5  2'Thema . . . . . :'
     A            AXID      R     A  B  5 20REFFLD(AXID APG/PAJXTHM)
     A  43                                  DSPATR(HI)
     A  43                                  DSPATR(PR)
     A  41                              5 62'Neuanlage'
     A                                      COLOR(RED)
     A  44                              6  2'Unterthema  . . :'
     A  44        AXKEY     R     A  B  6 20REFFLD(AXKEY APG/PAJXTHM)
     A  42                                  DSPATR(HI)
     A  42                                  DSPATR(PR)
     A                                  8  2'Kommentar . . . :'
     A            AXKOM1    R   60   B  8 20REFFLD(AXKOM1 APG/PAJXTHM)
     A                                      CHECK(LC)
     A            AXKOM2    R   60   B  9 20REFFLD(AXKOM2 APG/PAJXTHM)
     A                                      CHECK(LC)
     A            AXKOM3    R   60   B 10 20REFFLD(AXKOM3 APG/PAJXTHM)
     A                                      CHECK(LC)
     A                                 12  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A            SFT0003       76A  O 15  4DSPATR(HI)
     A*            17:14:43    QPGMR       REL-R07M00  5714-UT1
     A*                                     OVERLAY
     A          R FMT04
     A*%%TS  SD  20090604  211228  APG         REL-V5R4M0  5722-WDS
     A                                 23  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 23 34'F9=Beschreibung pflegen'
     A                                      COLOR(BLU)
     A                                 23 59'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  20090127  183236  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 19 13'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
