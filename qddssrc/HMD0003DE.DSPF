     A*%%TS  SD  20110703  224831  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* xxx                                                           *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  11.01.2009                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE EINES PROGRAMMS                      *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20110703  223819  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A            PTLFDN    R        H      REFFLD(PTLFDN APG/PTAGLIB)
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            AUSW           1A  B 23  2VALUES(' ' '2' '4' '5')
     A            PTTAG     R        O 23  4REFFLD(PTTAG APG/PTAGLIB)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20110703  224831  APG         REL-V5R4M0  5722-WDS
     A  *DS4                                SFLLIN(0003)
     A                                      SFLSIZ(0022)
     A                                      SFLPAG(0021)
     A                                      CA05(05)
     A                                      CF08(08)
     A                                      CA09(09)
     A                                      CA12(12)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            PLSEU2    R        H      REFFLD(PLSEU2 APG/PPGMLIB)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A  41                              4115'Neuanlage'
     A                                      COLOR(RED)
     A                                  5  2'Programm  . . . :'
     A            PLFILE    R     A  B  5 20REFFLD(PLFILE APG/PPGMLIB)
     A  42                                  DSPATR(HI)
     A  42                                  DSPATR(PR)
     A                                  5 32'(z.B. LGPGM18)'
     A                                      COLOR(BLU)
     A                                  6  2'Bibliothek  . . :'
     A            PLLIB     R     A  B  6 20REFFLD(PLLIB APG/PPGMLIB)
     A  42                                  DSPATR(HI)
     A  42                                  DSPATR(PR)
     A                                  6 32'(z.B. LFSPGMV7)'
     A                                      COLOR(BLU)
     A                                  8  2'Beschreibung  . :'
     A            PLMTXT    R        O  8 20REFFLD(PLMTXT APG/PPGMLIB)
     A                                      COLOR(BLU)
     A                                  9  2'Kommentar . . . :'
     A            PLKOM1    R   60   B  9 20REFFLD(PLKOM1 APG/PPGMLIB)
     A                                      CHECK(LC)
     A            PLKOM2    R   60   B 10 20REFFLD(PLKOM2 APG/PPGMLIB)
     A                                      CHECK(LC)
     A            PLKOM3    R   60   B 11 20REFFLD(PLKOM3 APG/PPGMLIB)
     A                                      CHECK(LC)
     A                                 19  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                 20  4'4=Löschen'
     A                                      COLOR(BLU)
     A            SFT0004A      10A  O 22  4DSPATR(HI)
     A            SFT0004B      10A  O 22 19DSPATR(HI)
     A            SFT0004C      10A  O 22 34DSPATR(HI)
     A            SFT0004D      10A  O 22 49DSPATR(HI)
     A            SFT0004E      10A  O 22 64DSPATR(HI)
     A            SFT0004F      10A  O 22 79DSPATR(HI)
     A            SFT0004G      10A  O 22 94DSPATR(HI)
     A            SFT0004H      10A  O 22109DSPATR(HI)
     A*            17:14:43    QPGMR       REL-R07M00  5714-UT1
     A*                                     OVERLAY
     A          R FMT04
     A*%%TS  SD  20110703  223620  APG         REL-V5R4M0  5722-WDS
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F8=Neuer Tag'
     A                                      COLOR(BLU)
     A                                 26 48'F9=Beschreibung pflegen'
     A                                      COLOR(BLU)
     A                                 26 73'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  20090127  183236  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 19 13'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
