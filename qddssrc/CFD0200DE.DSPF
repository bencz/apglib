     A*%%TS  SD  20110906  160029  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* ANZEIGE PROGRAMMTEXTE                                         *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  17.08.2009                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE PROGRAMMTEXTE                        *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A          R FMT01
     A*%%TS  SD  20110906  160029  APG         REL-V5R4M0  5722-WDS
     A  03                                  CSRLOC(CURY       CURX)
     A                                      CA12(12)
     A                                      CA21(21)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A*
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                 21  2'Sprache  . . . . .?:'
     A            TXSPR     R        B 21 23REFFLD(TXSPR APG/PCFGTXT)
     A                                 22  2'Text-ID  . . . . . :'
     A            TXID      R        B 22 23REFFLD(TXID APG/PCFGTXT)
     A                                 24  2'Freitextselektion  :'
     A            TXTSEL        30A  B 24 23
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20090817  174832  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            TXSPR     R        H      REFFLD(TXSPR APG/PCFGTXT)
     A            AUSW           1A  B 10  2VALUES(' ' '1' '4')
     A            TXID      R        O 10  4REFFLD(TXID APG/PCFGTXT)
     A  41                                  COLOR(RED)
     A            TXTEXT    R        O 10 15REFFLD(TXTEXT APG/PCFGTXT)
     A  41                                  COLOR(RED)
     A            TXKOML    R        O 11 15REFFLD(TXKOML APG/PCFGTXT)
     A                                      COLOR(BLU)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20090817  172940  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      CA05(05)
     A                                      CA08(08)
     A                                      CA12(12)
     A                                      CA13(13)
     A                                      CA21(21)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  6  4'1=Auswählen'
     A                                      COLOR(BLU)
     A                                  6 17'4=Löschen'
     A                                      COLOR(BLU)
     A            SFT0005       10A  O  9  4DSPATR(HI)
     A            SFT0006       50A  O  9 15DSPATR(HI)
     A*            17:14:43    QPGMR       REL-R07M00  5714-UT1
     A*                                     OVERLAY
     A*                                     OVERLAY
     A          R FMT04
     A*%%TS  SD  20110906  155913  APG         REL-V5R4M0  5722-WDS
     A*                                     OVERLAY
     A                                 26  2'F1=Zweite Zeile'
     A                                      COLOR(BLU)
     A                                 26 19'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 33'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 51'F8=Neuanlage'
     A                                      COLOR(BLU)
     A                                 26 65'F21=Kommandozeile'
     A                                      COLOR(BLU)
     A                                 26 84'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  19931217  173127  HEH         REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                 15 39'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
