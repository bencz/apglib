     A*%%TS  SD  20111109  130943  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* WARTUNG TEXTE                                                 *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  03.03.2011                                   *
     A*                                                               *
     A* FUNKTION      :  WARTUNG TEXTE                                *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A                                      CA12(12)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20111109  072004  APG         REL-V5R4M0  5722-WDS
     A                                      SFLNXTCHG
     A            FTFILE    R        H      REFFLD(FTFILE APG/PTXTFIT)
     A            FTKEY     R        H      REFFLD(FTKEY APG/PTXTFIT)
     A            FTLFPO    R        H      REFFLD(FTLFPO APG/PTXTFIT)
     A            LEERZ          1A  H
     A            FTTEXT    R        B 12  2REFFLD(FTTEXT APG/PTXTFIT)
     A                                      SFLCSRPRG
     A                                      CHECK(LC)
     A  70                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A            AUSW           1A  B 12 83VALUES(' ' '1' '4')
     A                                      SFLCSRPRG
     A  50                                  DSPATR(PR)
     A            DT_M          10   O 12 85
     A            TI_M           8   O 12 96
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20111109  072004  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  03                                  CSRLOC(CURY       CURX)
     A                                      CA05(05)
     A                                      CA21(21)
     A                                      BLINK
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            PGMTYP         8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                  3  2'Anzahl  . :'
     A            ANZREC         4Y 0O  3 14COLOR(RED)
     A                                      EDTCDE(3)
     A                                  5  2'Datei . . . . . :'
     A            FTFILE    R        O  5 20REFFLD(FTFILE APG/PTXTFIT)
     A            FTKEY     R        O  5 31REFFLD(FTKEY APG/PTXTFIT)
     A                                      COLOR(BLU)
     A                                  8  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken'
     A                                      COLOR(BLU)
     A                                  9  4'1=Zeile einfügen'
     A                                      COLOR(BLU)
     A  50                                  DSPATR(ND)
     A                                  9 22'4=Zeile löschen'
     A                                      COLOR(BLU)
     A  50                                  DSPATR(ND)
     A            SFT0006       77A  O 11  2DSPATR(HI)
     A            SFT0011       10   O 11 85DSPATR(HI)
     A            SFT0058        8   O 11 96DSPATR(HI)
     A*--------------------------------------------------------------
     A          R FMT04
     A*%%TS  SD  20111109  130943  APG         REL-V5R4M0  5722-WDS
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 26 49'F21=Alles löschen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT05
     A*%%TS  SD  19931217  173127  HEH         REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                 15 39'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
