     A*%%TS  SD  20110912  071701  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* WINDOW ZUR SCHLÜSSELSELEKTION                                 *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  09.09.2011                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE RECORDS                              *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03 'Ende')
     A                                      CA12(12 'Zurück')
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20110912  071701  APG         REL-V5R4M0  5722-WDS
     A            NUMBERORIG    65A  H
     A            HIDCONTENT    57A  H
     A            AUSW           1A  B 10  2
     A            FLDNAME       10A  O 10  4
     A  50                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A            FLDCONTENT    57G  O 10 15CCSID(13488 *MIN)
     A  51
     AO 52                                  DSPATR(RI)
     A  50                                  DSPATR(HI)
     A            FLDDESC       50A  O 10 73
     A  52                                  DSPATR(RI)
     A            FLDTYP         8A  O 10124
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20110909  150200  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      CA05(05 'AKT')
     A                                      BLINK
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A  70                                  SFLMSGID(LFS0946 *LIBL/LFSMSGF)
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15EDTCDE(4)
     A                                      COLOR(RED)
     A                                  4  2'Datei  . . :'
     A            ANZEIGETYP    30A  O  4 15TEXT('KENNUNG')
     A                                  5  2'Feldsuche  :'
     A            FLDSTART      10A  B  5 15DSPATR(PC)
     A                                  5 27'Freitextsel:'
     A            FLDSEARCH     50A  B  5 40
     A                                  7  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken'
     A                                      COLOR(BLU)
     A            SFT0051       10A  O  9  4DSPATR(HI)
     A            SFT0056       11A  O  9 15DSPATR(HI)
     A            SFT0057       14A  O  9 73DSPATR(HI)
     A            SFT0024        3A  O  9126DSPATR(HI)
     A*--------------------------------------------------------------
     A          R FMT04
     A*%%TS  SD  20100118  174515  BGE         REL-V6R1M0  5761-WDS
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  20100118  172130  GGO         REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                 15 39'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
