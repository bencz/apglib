     A*%%TS  SD  20110119  071107  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* PFLEGE DER ZÄHLERSTÄNDE                                       *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  06.01.2011                                   *
     A*                                                               *
     A* FUNKTION      :  ZÄHLERSTÄNDE/KAFFEEKASSE                     *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20110119  071107  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A            AUSW           1A  B 10  2VALUES(' ' '1' '4')
     A            KKTYP     R        O 10  4REFFLD(KKTYP APG/PKDKKAS)
     A            KKIDEN    R        O 10 15REFFLD(KKIDEN APG/PKDKKAS)
     A            KKVMEN    R    8 00O 10 36REFFLD(KKVMEN APG/PKDKKAS)
     A                                      EDTCDE(3)
     A            KKBMEN    R    8 00B 10 45REFFLD(KKBMEN APG/PKDKKAS)
     A                                      EDTCDE(3)
     A                                 10 54'*'
     A                                      COLOR(BLU)
     A            KKEPR     R    5 02O 10 56REFFLD(KKEPR APG/PKDKKAS)
     A                                      EDTCDE(3)
     A                                 10 63'='
     A                                      COLOR(BLU)
     A            GESAMT         8Y 2O 10 65EDTCDE(3)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20110119  070727  APG         REL-V5R4M0  5722-WDS
     A                                      CA05(05)
     A                                      CA08(08)
     A                                      CA12(12)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A                                      TEXT('REL.-RECORD-NR FEHLERH.-SATZ')
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            PGMTYP         8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  6  4'1=Auswählen'
     A                                      COLOR(BLU)
     A                                  6 17'4=Löschen'
     A                                      COLOR(BLU)
     A            SFT0011       10A  O  9  4DSPATR(HI)
     A            SFT0010       10A  O  9 15DSPATR(HI)
     A          R FMT04
     A*%%TS  SD  20110119  070727  APG         REL-V5R4M0  5722-WDS
     A            GSUMME         8  2O 25 65EDTCDE(3)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  20101111  170317  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 15 39'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
