     A*%%TS  SD  20100204  071336  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* ANZEIGE SYNTAX-ÜBERSCHREIBUNGEN                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  02.02.2010                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE SYNTAX-BEFEHLE                       *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20100202  212530  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            TYUSER    R        H      REFFLD(TYUSER APG/PTYPHGH)
     A            AUSW           1A  B  9  2VALUES(' ' '1' '4')
     A            TYTYP     R        O  9  4REFFLD(TYTYP APG/PTYPHGH)
     A            TYOVR     R        O  9 15REFFLD(TYOVR APG/PTYPHGH)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20100204  071336  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05(05)
     A                                      CA08(08)
     A                                      CA12(12)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A                                      TEXT('REL.-RECORD-NR FEHLERH.-SATZ')
     A                                  1  2'CRD0022DE'
     A                                      COLOR(BLU)
     A                                  1 13'FMT03'
     A                                      COLOR(BLU)
     A            MAINUEB       38A  O  1 20DSPATR(HI)
     A            PGMTYP         8A  O  1 62COLOR(BLU)
     A                                  1 72SYSNAME
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A            LANGID         3A  O  2 13MSGID(SP0 0001 QTEMP/APGSPRACH)
     A                                      COLOR(BLU)
     A            SUBUEB        38A  O  2 20DSPATR(HI)
     A                                  2 62DATE(*SYS *Y)
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  6  4'1=Auswählen'
     A                                      COLOR(BLU)
     A                                  6 17'4=Löschen'
     A                                      COLOR(BLU)
     A            SFT0024       10A  O  8  4DSPATR(HI)
     A            SFT0027       10A  O  8 15DSPATR(HI)
     A*            17:14:43    QPGMR       REL-R07M00  5714-UT1
     A*                                     OVERLAY
     A*                                     OVERLAY
     A*                                     OVERLAY
     A*                                     OVERLAY
     A          R FMT04
     A*%%TS  SD  20100202  205949  APG         REL-V5R4M0  5722-WDS
     A                                 23  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 23 34'F8=Neuanlage'
     A                                      COLOR(BLU)
     A                                 23 48'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  19931217  173127  HEH         REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                 15 13'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
     A          R FMT06
     A*%%TS  SD  20100202  212721  APG         REL-V5R4M0  5722-WDS
     A                                      CA12(12)
     A  03                                  CSRLOC(CURY       CURX)
     A  01        ERRMSG        78   M
     A            CURX           3S 0H
     A            CURY           3S 0H
     A                                  1  2'CRD0022DE'
     A                                      COLOR(BLU)
     A                                  1 13'FMT06'
     A                                      COLOR(BLU)
     A            MAINUEB       38A  O  1 20DSPATR(HI)
     A            PGMTYP         8A  O  1 62COLOR(BLU)
     A                                  1 72SYSNAME
     A                                      COLOR(BLU)
     A                                  2  2USER
     A            LANGID         3A  O  2 13MSGID(SP0 0001 QTEMP/APGSPRACH)
     A                                      COLOR(BLU)
     A            SUBUEB        38A  O  2 20DSPATR(HI)
     A                                  2 62DATE(*SYS *Y)
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                 19  2'Sourcentyp . . . . :'
     A            TYTYP     R        B 19 23REFFLD(TYTYP APG/PTYPHGH)
     A  41                                  DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                 20  2'Sourcentyp verwend.:'
     A            TYOVR     R        B 20 23REFFLD(TYOVR APG/PTYPHGH)
     A                                 23  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 23 16'F12=Abbrechen'
     A                                      COLOR(BLU)
