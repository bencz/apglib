     A*%%TS  SD  20121130  073159  APG         REL-V5R4M0  5722-WDS
     A*ˆKEINE F4/F6 MEHR VERWENDEN !!! RECORD WIE XXD0015DE
     A*---------------------------------------------------------------*
     A* ANZEIGE OPL/TODO-LISTE                                        *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  28.01.2011                                   *
     A*                                                               *
     A* FUNKTION      :  ANZEIGE OPL-DATEN                            *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A  *DS3                                MSGLOC(24)
     A  *DS4                                MSGLOC(27)
     A                                      PRINT
     A                                      HELP
     A                                      HLPTITLE('XXD0015DE')
     A                                      CA05(05)
     A                                      CA12(12)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20121130  073159  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A            A_DT_CRT8      1A  P
     A            A_OPTEXT       1A  P
     A            A_OPPRZT       1A  P
     A            A_ANZPRIO      1A  P
     A            OPLFDN    R        H      REFFLD(OPLFDN APG/POPLLST)
     A            OPSHRN    R        H      REFFLD(OPSHRN APG/POPLLST)
     A            OPDATE    R        H      REFFLD(OPDATE APG/POPLLST)
     A            OPUSER    R        H      REFFLD(OPUSER APG/POPLLST)
     A            OPTYPE    R        H      REFFLD(OPTYPE APG/POPLLST)
     A            OPTTYP    R        H      REFFLD(OPTTYP APG/POPLLST)
     A            OPTID     R        H      REFFLD(OPTID APG/POPLLST)
     A            OPPRIO    R        H      REFFLD(OPPRIO APG/POPLLST)
     A            OPIMPT    R        H      REFFLD(OPIMPT APG/POPLLST)
     A            OPCRYP    R        H      REFFLD(OPCRYP APG/POPLLST)
     A            OPCRYT    R        H      REFFLD(OPCRYT APG/POPLLST)
     A            AUSW           1A  B  6  2VALUES(' ' '1' '2' '4' '8' '9' 'S' -
     A                                      'C' 'M' 'P' 'Y' 'I' 'E' 'D')
     A            DT_CRT8        8A  O  6  4
     A  02                                  DSPATR(&A_DT_CRT8)
     A            OPTEXT    R   57   B  6 13REFFLD(OPTEXT APG/POPLLST)
     A                                      CHGINPDFT
     A  02                                  DSPATR(&A_OPTEXT)
     A                                      CHECK(LC)
     A            OPPRZT    R     Y  B  6 71REFFLD(OPPRZT APG/POPLLST)
     A                                      CHGINPDFT
     A  02                                  DSPATR(&A_OPPRZT)
     A                                      EDTCDE(4)
     A                                  6 75'%'
     A                                      COLOR(BLU)
     A                                  7  4'Letzte Änderung:'
     A                                      COLOR(BLU)
     A            DT_LST         8A  O  7 21COLOR(BLU)
     A            TI_LST         8A  O  7 30COLOR(BLU)
     A            LSTUSR        10A  O  7 39COLOR(BLU)
     A            ANZPRIO   R        O  6 77REFFLD(OPPRIO APG/POPLLST)
     A                                      EDTCDE(4)
     A  02                                  DSPATR(&A_ANZPRIO)
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20110906  152724  APG         REL-V5R4M0  5722-WDS
     A  *DS3                                SFLSIZ(0008)
     A  *DS4                                SFLSIZ(0010)
     A  *DS3                                SFLPAG(0007)
     A  *DS4                                SFLPAG(0009)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      CA02(02)
     A                                      CA03(03)
     A                                      CA13(13)
     A                                      CA14(14)
     A                                      CA15(15)
     A                                      CA20(20)
     A                                      CA21(21)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            MAINUEB       40A  O  1 15DSPATR(HI)
     A  41        SYS_CMP       21A  O  1 59COLOR(BLU)
     A*€          SYS_CMP       21A  O  1 93COLOR(BLU)
     A N41        SYS_PGMTYP     8A  O  1 62COLOR(BLU)
     A N41                              1 72SYSNAME
     A                                      COLOR(BLU)
     A            SYS_FMT       10A  O  2  2COLOR(BLU)
     A*€          SYS_USER      10A  O  2  2
     A*€          SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        40A  O  2 15
     A  41        SYS_CMT       21A  O  2 59COLOR(BLU)
     A*€          SYS_CMT       21A  O  2 93COLOR(BLU)
     A N41        DT_SYS         8A  O  2 62
     A N41        TI_SYS         8A  O  2 72
     A                                  3  2'Anzahl:'
     A            ANZREC         4Y 0O  3 10COLOR(RED)
     A                                      EDTCDE(4)
     A                                  3 15'Anzeige:'
     A            ANZMOD        20A  O  3 24COLOR(BLU)
     A                                  3 45'Selektion:'
     A            SELEKT        19A  B  3 56COLOR(PNK)
     A            SFT0011        8A  O  5  4DSPATR(HI)
     A            SFT0006       57A  O  5 13DSPATR(HI)
     A            SFT0047        3A  O  5 71DSPATR(HI)
     A*--------------------------------------------------------------
     A*€                                    OVERLAY
     A          R FMT04
     A*%%TS  SD  20110323  095103  APG         REL-V5R4M0  5722-WDS
     A  03                                  CSRLOC(CURY       CURX)
     A          H                           HLPPNLGRP('XXH0015DE/FMT03' XXH0015-
     A                                      DE)
     A  *DS3                                HLPARA(01 001 24 080)
     A  *DS4                                HLPARA(01 001 27 132)
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A                                 22  1'=>'
     A  40        DT_CRT        10A  B 22  4
     A N40        TXTLNG        71A  B 22  4
     A                                      CHECK(LC)
     A  40        OPTYPE    R   11   O 22 15REFFLD(OPTYPE APG/POPLLST)
     A                                      COLOR(BLU)
     A  40        OPTEXT    R   48   B 22 27REFFLD(OPTEXT APG/POPLLST)
     A                                      CHECK(LC)
     A                                 23  1'F2=Kategorie wechseln'
     A                                      COLOR(BLU)
     A                                 23 24'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 23 39'F13=Alle anzeigen'
     A                                      COLOR(BLU)
     A                                 23 58'F20=Infos'
     A  42                                  DSPATR(BL)
     A  42                                  COLOR(RED)
     A N42                                  COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT05
     A*%%TS  SD  20110224  130802  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 15 14'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  19940426  153744  UB          REL-V2R2M0  5738-PW1
     A                                      ASSUME
     A                                 01 02' '
     A                                      DSPATR(ND)
