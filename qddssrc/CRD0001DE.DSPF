     A*%%TS  SD  20090826  182623  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* CODEREVIEW                                                    *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  09.08.2009                                   *
     A*                                                               *
     A* FUNKTION      :  ZENTRALES CODE-REVIEW-PGM                    *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A          R FMT01
     A*%%TS  SD  20090809  174104  APG         REL-V5R4M0  5722-WDS
     A  03                                  CSRLOC(CURY       CURX)
     A                                      CA08(08)
     A                                      CA09(09)
     A                                      CA12(12)
     A                                      CA18(18)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A*
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A                                  1  2'CRD0001DE'
     A                                      COLOR(BLU)
     A                                  1 13'FMT01'
     A                                      COLOR(BLU)
     A            MAINUEB       38A  O  1 20DSPATR(HI)
     A            PGMTYP         8A  O  1 62COLOR(BLU)
     A                                  1 72SYSNAME
     A                                      COLOR(BLU)
     A                                  2  2USER
     A            LANGID         3A  O  2 13MSGID(SP0 0001 QTEMP/APGSPRACH)
     A                                      COLOR(BLU)
     A            SUBUEB        38A  O  2 20DSPATR(HI)
     A                                  2 62DATE(*SYS *Y)
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                 13  2'Bibliothek . . . . :'
     A            CRLIB     R     A  B 13 23REFFLD(CRLIB APG/PCHKSRC)
     A                                 14  2'Datei  . . . . . . :'
     A            CRFILE    R     A  B 14 23REFFLD(CRFILE APG/PCHKSRC)
     A                                 15  2'Teildatei  . . . . :'
     A            CRMBR     R     A  B 15 23REFFLD(CRMBR APG/PCHKSRC)
     A                                 17  2'Programmierer  . . :'
     A            CRCUSR    R     A  B 17 23REFFLD(CRCUSR APG/PCHKSRC)
     A                                 18  2'P3-Nummer  . . . . :'
     A            CRPGMA    R     Y  B 18 23REFFLD(CRPGMA APG/PCHKSRC)
     A                                      EDTCDE(4)
     A                                 19  2'Datum  . . . . . . :'
     A            CRCDTA    R     A  B 19 23REFFLD(CRCDTA APG/PCHKSRC)
     A                                 21  2'Beschreibung . . . :'
     A            TXTSEL        30A  B 21 23
     A                                 23  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 23 34'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20090826  182623  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A* ATTRIBUTFELDER FÜR ALLE FELDER MIT FEHLERAUSGABE
     A            CRLFDN    R        H      REFFLD(CRLFDN APG/PCHKSRC)
     A            CRLIB     R        H      REFFLD(CRLIB APG/PCHKSRC)
     A            CRFILE    R        H      REFFLD(CRFILE APG/PCHKSRC)
     A            CRMBR     R        H      REFFLD(CRMBR APG/PCHKSRC)
     A            CRLINE    R        H      REFFLD(CRLINE APG/PCHKSRC)
     A            AUSW           1A  B 10  2VALUES(' ' '1' '2' '5')
     A            CRSECN    R        O 10  4REFFLD(CRSECN APG/PCHKSRC)
     A                                      EDTCDE(4)
     A            CRFID     R        O 10 12REFFLD(CRFID APG/PCHKSRC)
     A            CRFTXT    R   57   O 10 23REFFLD(CRFTXT APG/PCHKSRC)
     A            DT_SRC        10   O 11 12COLOR(BLU)
     A            CRFTX2    R   57   O 11 23REFFLD(CRFTX2 APG/PCHKSRC)
     A                                      COLOR(BLU)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20090820  193024  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      CA05(05)
     A                                      CA08(08)
     A                                      CA09(09)
     A                                      CA12(12)
     A                                      CA13(13)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A                                      TEXT('REL.-RECORD-NR FEHLERH.-SATZ')
     A                                  1  2'CRD0001DE'
     A                                      COLOR(BLU)
     A                                  1 13'FMT03'
     A                                      COLOR(BLU)
     A            MAINUEB       38A  O  1 20DSPATR(HI)
     A            PGMTYP         8A  O  1 62COLOR(BLU)
     A                                  1 72SYSNAME
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A            LANGID         3A  O  2 13MSGID(SP0 0001 QTEMP/APGSPRACH)
     A                                      COLOR(BLU)
     A            SUBUEB        38A  O  2 20DSPATR(HI)
     A                                  2 62DATE(*SYS *Y)
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  6  4'1=Auswählen'
     A                                      COLOR(BLU)
     A                                  6 17'2=Ändern'
     A                                      COLOR(BLU)
     A                                  6 27'5=Anzeigen'
     A                                      COLOR(BLU)
     A            SFT0007        7A  O  9  4DSPATR(HI)
     A            SFT0005       10A  O  9 12DSPATR(HI)
     A            SFT0003       57A  O  9 23DSPATR(HI)
     A*            17:14:43    QPGMR       REL-R07M00  5714-UT1
     A*                                     OVERLAY
     A*                                     OVERLAY
     A*                                     OVERLAY
     A*                                     OVERLAY
     A          R FMT04
     A*%%TS  SD  20090819  175216  APG         REL-V5R4M0  5722-WDS
     A*                                     OVERLAY
     A                                 22  2'F1=Zweite Zeile'
     A                                      COLOR(BLU)
     A                                 22 19'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 22 33'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 22 51'F8=SEU starten'
     A                                      COLOR(BLU)
     A                                 23  2'F9=Kommandozeile'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Abbrechen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  19931217  173127  HEH         REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                 15 13'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
