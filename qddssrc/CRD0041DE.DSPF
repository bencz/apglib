     A*%%TS  SD  20120113  094821  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* ANGABE EINER SPOOL-DATEI                                      *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  13.01.2012                                   *
     A*                                                               *
     A* FUNKTION      :  EINGABE EINER SPOOL-DATEI FÜR MAPDTA         *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A  *DS4                                MSGLOC(27)
     A  *DS3                                MSGLOC(24)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA12(12 'Abbrechen')
     A*--------------------------------------------------------------
     A          R FMT01
     A*%%TS  SD  20120113  094821  APG         REL-V5R4M0  5722-WDS
     A  *DS4                                WINDOW(FMT02)
     A  *DS3                                WINDOW(FMT02)
     A                                      OVERLAY
     A                                  1  1'CRD0041DE'
     A                                      COLOR(BLU)
     A            MAINUEB       20A  O  1 11DSPATR(HI)
     A                                  3  1'Spooldatei :'
     A            SPLNAM        10A  B  3 14
     A                                  4  1'Jobname  . :'
     A            JOBNAM        10A  B  4 14
     A                                  6  1'Optionen . :'
     A                                  7  2'00er Stufe:'
     A            FKST00         1Y 0B  7 14EDTCDE(3)
     A                                      VALUES(0 1)
     A                                  7 17'(0=nein/1=ja)'
     A                                      COLOR(BLU)
     A                                  8  2'10er Stufe:'
     A            FKST10         1Y 0B  8 14EDTCDE(3)
     A                                      VALUES(0 1)
     A                                  8 17'(0=nein/1=ja)'
     A                                      COLOR(BLU)
     A                                  9  2'20er Stufe:'
     A            FKST20         1Y 0B  9 14EDTCDE(3)
     A                                      VALUES(0 1)
     A                                  9 17'(0=nein/1=ja)'
     A                                      COLOR(BLU)
     A                                 10  2'30er Stufe:'
     A            FKST30         1Y 0B 10 14EDTCDE(3)
     A                                      VALUES(0 1)
     A                                 10 17'(0=nein/1=ja)'
     A                                      COLOR(BLU)
     A                                 12  1'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT02
     A*%%TS  SD  20010214  094701  JPF         REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A  *DS3                                WINDOW(8 17 13 43)
     A  *DS4                                WINDOW(8 73 13 43)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  20000821  124406  KAB         REL-V4R2M0  5769-PW1
     A                                      ASSUME
     A                                  1  2' '
     A                                      DSPATR(ND)
