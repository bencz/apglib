     A*%%TS  SD  20130613  070346  APG         REL-V6R1M0  5761-WDS
     A*---------------------------------------------------------------*
     A* DATENEINGABE FÜR START/READ-NEXT                              *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  03.06.2013                                   *
     A*                                                               *
     A* FUNKTION      :  EINGABE VON DATEN IM WINDOW                  *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A  *DS4                                MSGLOC(27)
     A  *DS3                                MSGLOC(24)
     A                                      PRINT
     A                                      CA05(05)
     A                                      CA12(12)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20130613  070346  APG         REL-V6R1M0  5761-WDS
     A            AUSW           1A  B  4  2VALUES(' ' '4')
     A            KEYVAL        20A  O  4  4
     A            OPTVAL        20A  B  4 25
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20130613  070346  APG         REL-V6R1M0  5761-WDS
     A  *DS4                                SFLSIZ(0007)
     A  *DS3                                SFLSIZ(0007)
     A  *DS4                                SFLPAG(0006)
     A  *DS3                                SFLPAG(0006)
     A  *DS4                                WINDOW(FMT04)
     A  *DS3                                WINDOW(FMT04)
     A  03                                  CSRLOC(CURY       CURX)
     A                                      BLINK
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A                                  1  2'XXD0053DE'
     A                                      COLOR(BLU)
     A            MAINUEB       21A  O  1 13DSPATR(HI)
     A                                  2  2'4=Löschen'
     A                                      COLOR(BLU)
     A            KEYVAL        20A  B  3  4
     A            OPTVAL        20A  B  3 25
     A*--------------------------------------------------------------
     A          R FMT04
     A*%%TS  SD  20090202  103144  APG         REL-V5R4M0  5722-WDS
     A                                      WINDOW(12 20 11 56)
     A                                 10  2'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT05
     A*%%TS  SD  20090127  224738  APG         REL-V5R4M0  5722-WDS
     A                                      WINDOW(FMT04)
     A                                      OVERLAY
     A                                  7  2'Keine Daten angelegt'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  19940426  153744  UB          REL-V2R2M0  5738-PW1
     A                                      ASSUME
     A                                 01 02' '
     A                                      DSPATR(ND)
