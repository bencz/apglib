     A*%%TS  SD  20110906  101111  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* KÜCHEN UND LAGERDIENST                                        *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  28.08.2009                                   *
     A*                                                               *
     A* FUNKTION      :  PLANUNG KÜCHENDIENST                         *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      MSGLOC(27)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A          R FMT01
     A*%%TS  SD  20110817  131149  APG         REL-V5R4M0  5722-WDS
     A  03                                  CSRLOC(CURY       CURX)
     A                                      CA08(08)
     A                                      CA12(12)
     A                                      CA18(18)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A*
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            PGMTYP         8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                 18  2'User . . . . . . . :'
     A            CFKEY     R   10   B 18 23REFFLD(CFKEY APG/PCONFIG)
     A                                 19  2'Ausbildungsbeginn  :'
     A            CFNM04    R        B 19 23REFFLD(CFNM04 APG/PCONFIG)
     A                                      EDTCDE(4)
     A                                 21  2'Berufsschule . . .?:'
     A            CFIN01    R   10   B 21 23REFFLD(CFIN01 APG/PCONFIG)
     A                                 22  2'Klasse . . . . . .?:'
     A            CFIN02    R   10   B 22 23REFFLD(CFIN02 APG/PCONFIG)
     A                                 24  2'Ausbildungsberuf . :'
     A            CFIN03    R   10   B 24 23REFFLD(CFIN03 APG/PCONFIG)
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20110816  131110  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A            A_AUSW         1A  P
     A            A_CFKEY        1A  P
     A            A_CFFK01       1A  P
     A            A_CFFK02       1A  P
     A            A_CFFK03       1A  P
     A            A_CFFK04       1A  P
     A            A_CFFK05       1A  P
     A            A_KW01         1A  P
     A            A_KW02         1A  P
     A            A_KW03         1A  P
     A            A_KW04         1A  P
     A            A_KW05         1A  P
     A            A_KW06         1A  P
     A            A_KW07         1A  P
     A            CFIN01    R        H      REFFLD(CFIN01 APG/PCONFIG)
     A            CFIN02    R        H      REFFLD(CFIN02 APG/PCONFIG)
     A            AUSW           1A  B 10  2VALUES(' ' '1' '8' '9' 'T')
     A  02                                  DSPATR(&A_AUSW)
     A            CFKEY     R   10   O 10  4REFFLD(CFKEY APG/PCONFIG)
     A  02                                  DSPATR(&A_CFKEY)
     A            CFFK01    R        O 10 15REFFLD(CFFK01 APG/PCONFIG)
     A  02                                  DSPATR(&A_CFFK01)
     A                                      EDTCDE(3)
     A            CFFK02    R        O 10 17REFFLD(CFFK02 APG/PCONFIG)
     A  02                                  DSPATR(&A_CFFK02)
     A                                      EDTCDE(3)
     A            CFFK03    R        O 10 19REFFLD(CFFK03 APG/PCONFIG)
     A  02                                  DSPATR(&A_CFFK03)
     A                                      EDTCDE(3)
     A            CFFK04    R        O 10 21REFFLD(CFFK04 APG/PCONFIG)
     A  02                                  DSPATR(&A_CFFK04)
     A                                      EDTCDE(3)
     A            CFFK05    R        O 10 23REFFLD(CFFK05 APG/PCONFIG)
     A  02                                  DSPATR(&A_CFFK05)
     A                                      EDTCDE(3)
     A            KW01          10A  O 10 25
     A  02                                  DSPATR(&A_KW01)
     A                                 10 37'|'
     A                                      COLOR(BLU)
     A            KW02          10A  O 10 39
     A  02                                  DSPATR(&A_KW02)
     A                                 10 51'|'
     A                                      COLOR(BLU)
     A            KW03          10A  O 10 53
     A  02                                  DSPATR(&A_KW03)
     A                                 10 65'|'
     A                                      COLOR(BLU)
     A            KW04          10A  O 10 67
     A  02                                  DSPATR(&A_KW04)
     A                                 10 79'|'
     A                                      COLOR(BLU)
     A            KW05          10A  O 10 81
     A  02                                  DSPATR(&A_KW05)
     A                                 10 93'|'
     A                                      COLOR(BLU)
     A            KW06          10A  O 10 95
     A  02                                  DSPATR(&A_KW06)
     A                                 10107'|'
     A                                      COLOR(BLU)
     A            KW07          10A  O 10109
     A  02                                  DSPATR(&A_KW07)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20110906  101111  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      CA05(05)
     A                                      CA12(12)
     A                                      CA19(19)
     A                                      CA20(20)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A                                      SFLDROP(CA01)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A            A_HED001       1A  P
     A            A_HED002       1A  P
     A            A_HED003       1A  P
     A            A_HED004       1A  P
     A            A_HED005       1A  P
     A            A_HED006       1A  P
     A            A_HED007       1A  P
     A            SYS_DSP       10A  O  1  2COLOR(BLU)
     A            SYS_FMT        6A  O  1 13COLOR(BLU)
     A            MAINUEB       72A  O  1 20DSPATR(HI)
     A            SYS_CMP       21A  O  1 93COLOR(BLU)
     A            SYS_PGMTYP     8A  O  1115COLOR(BLU)
     A                                  1124SYSNAME
     A                                      COLOR(BLU)
     A            SYS_USER      10A  O  2  2
     A            SYS_LANG       3A  O  2 13COLOR(BLU)
     A            SUBUEB        72A  O  2 20DSPATR(HI)
     A            SYS_CMT       21A  O  2 93COLOR(BLU)
     A            DT_SYS         8A  O  2115
     A            TI_SYS         8A  O  2124
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A                                      EDTCDE(4)
     A                                  3 20'KW:'
     A            SHWKW          2Y 0O  3 24EDTCDE(4)
     A                                  5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  6  4'1=Checkliste'
     A                                      COLOR(BLU)
     A                                  6 18'8=Notizen'
     A                                      COLOR(BLU)
     A                                  6 29'9=Urlaub'
     A                                      COLOR(BLU)
     A                                  6 39'T=Telefonnummer'
     A                                      COLOR(BLU)
     A            HED001        11A  O  8 25
     A  02                                  DSPATR(&A_HED001)
     A            HED002        11A  O  8 39
     A  02                                  DSPATR(&A_HED002)
     A            HED003        11A  O  8 53
     A  02                                  DSPATR(&A_HED003)
     A            HED004        11A  O  8 67
     A  02                                  DSPATR(&A_HED004)
     A            HED005        11A  O  8 81
     A  02                                  DSPATR(&A_HED005)
     A            HED006        11A  O  8 95
     A  02                                  DSPATR(&A_HED006)
     A            HED007        11A  O  8109
     A  02                                  DSPATR(&A_HED007)
     A            SFT0011       10A  O  9  4DSPATR(HI)
     A            SFT0010        9A  O  9 15DSPATR(HI)
     A            SFT00501      11A  O  9 25DSPATR(HI)
     A            SFT00502      11A  O  9 39DSPATR(HI)
     A            SFT00503      11A  O  9 53DSPATR(HI)
     A            SFT00504      11A  O  9 67DSPATR(HI)
     A            SFT00505      11A  O  9 81DSPATR(HI)
     A            SFT00506      11A  O  9 95DSPATR(HI)
     A            SFT00507      11A  O  9109DSPATR(HI)
     A          R FMT04
     A*%%TS  SD  20110812  065929  APG         REL-V5R4M0  5722-WDS
     A                                 26  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 26 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 26 34'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 26 49'F19=Links'
     A                                      COLOR(BLU)
     A                                 26 60'F20=Rechts'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  20101111  170317  APG         REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                 15 39'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
