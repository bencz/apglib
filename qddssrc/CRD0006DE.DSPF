     A*%%TS  SD  20091229  161721  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* WINDOW ZUR SOURCE-ELEMENTSELEKTION                            *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  29.12.2009                                   *
     A*                                                               *
     A* FUNKTION      :  WINDOW ZUR EINGABE VON NOTIZEN               *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A  *DS4                                MSGLOC(27)
     A  *DS3                                MSGLOC(24)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA12(12 'Abbrechen')
     A*--------------------------------------------------------------
     A          R FMT01
     A*%%TS  SD  20091229  161721  APG         REL-V5R4M0  5722-WDS
     A  *DS4                                WINDOW(FMT02)
     A  *DS3                                WINDOW(FMT02)
     A                                      OVERLAY
     A                                  1  1'CRD0006DE'
     A                                      COLOR(BLU)
     A            MAINUEB       20A  O  1 11DSPATR(HI)
     A                                  3  1'Teildatei  :'
     A            SRCMBR        10   O  3 14
     A                                  4  1'Datei  . . :'
     A            SRCRCN        10   O  4 14
     A                                  5  1'Bibliothek :'
     A            SRCLIB        10   O  5 14
     A                                  6  1'Zeile  . . :'
     A            SRCSEQ         6Y 2O  6 14EDTCDE(2)
     A                                  8  1'Notiz  . . :'
     A            NOTE1         20A  B  8 14
     A  71                                  ERRMSGID(LFS0929 *LIBL/LFSMSGF)
     A                                      CHECK(LC)
     A            NOTE2         20A  B  9 14
     A  71                                  ERRMSGID(LFS0929 *LIBL/LFSMSGF)
     A                                      CHECK(LC)
     A                                 11  1'F12=Abbrechen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT02
     A*%%TS  SD  20010214  094701  JPF         REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A  *DS3                                WINDOW(8 17 12 43)
     A  *DS4                                WINDOW(8 73 12 43)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  20000821  124406  KAB         REL-V4R2M0  5769-PW1
     A                                      ASSUME
     A                                  1  2' '
     A                                      DSPATR(ND)
