     A*%%TS  SD  20100315  071101  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* BESTELLUNGSÜBERSICHT                                          *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  ANDREAS PIEGER                               *
     A*                                                               *
     A* ERSTELLT AM   :  11.03.2010                                   *
     A*                                                               *
     A* FUNKTION      :  BESTELLUNGSÜBERSICHT                         *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      ERRSFL
     A                                      PRINT
     A                                      CA03(03)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20100311  224648  APG         REL-V5R4M0  5722-WDS
     A  04                                  SFLNXTCHG
     A            KBLFDN    R        H      REFFLD(KBLFDN APG/PKDBST1)
     A            AUSW           1A  B  9  2VALUES(' ' '1' '2' '4' '8')
     A            KBBST1    R        O  9  4REFFLD(KBBST1 APG/PKDBST1)
     A            DT_B          10A  O  9 15
     A            KBIDEN    R        O  9 26REFFLD(KBIDEN APG/PKDBST1)
     A            KBBMEN    R        O  9 47REFFLD(KBBMEN APG/PKDBST1)
     A                                      EDTCDE(3)
     A            KBBUSR    R        O  9 60REFFLD(KBBUSR APG/PKDBST1)
     A            KBVSTS    R        O  9 71REFFLD(KBVSTS APG/PKDBST1)
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20100311  224648  APG         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05(05)
     A                                      CA08(08)
     A                                      CA12(12)
     A                                      CA14(14)
     A                                      BLINK
     A  03                                  CSRLOC(CURY       CURX)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A                                      TEXT('REL.-RECORD-NR FEHLERH.-SATZ')
     A                                  1  2'XXD0206DE'
     A                                      COLOR(BLU)
     A                                  1 13'FMT03'
     A                                      COLOR(BLU)
     A                                  1 13'FMT03'
     A                                      COLOR(BLU)
     A            MAINUEB       38A  O  1 20DSPATR(HI)
     A            PGMTYP         8A  O  1 62COLOR(BLU)
     A                                  1 72SYSNAME
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A            LANGID         3A  O  2 13MSGID(SP0 0001 QTEMP/APGSPRACH)
     A                                      COLOR(BLU)
     A            SUBUEB        38A  O  2 20DSPATR(HI)
     A                                  2 62DATE(*SYS *Y)
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  3  2'Anzahl . . :'
     A            ANZREC         4Y 0O  3 15COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2'Auswahl eingeben und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A                                  6  4'1=Auswählen'
     A                                      COLOR(BLU)
     A                                  6 17'2=Buchen'
     A                                      COLOR(BLU)
     A                                  6 27'4=Löschen'
     A                                      COLOR(BLU)
     A                                  6 38'8=Bestellung abschließen'
     A                                      COLOR(BLU)
     A            SFT0029       10A  O  8  4DSPATR(HI)
     A            SFT0011       10A  O  8 15DSPATR(HI)
     A            SFT0030       20A  O  8 26DSPATR(HI)
     A            SFT0031       12A  O  8 47DSPATR(HI)
     A            SFT0032       10A  O  8 60DSPATR(HI)
     A            SFT0033        1A  O  8 71DSPATR(HI)
     A          R FMT04
     A*%%TS  SD  20100315  071101  APG         REL-V5R4M0  5722-WDS
     A                                 23  2'F3=Verlassen'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Aktualisieren'
     A                                      COLOR(BLU)
     A                                 23 34'F8=Neuanlage'
     A                                      COLOR(BLU)
     A                                 23 48'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 23 63'F14=Alle Anzeigen'
     A                                      COLOR(BLU)
     A          R FMT05
     A*%%TS  SD  19931217  173127  HEH         REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                 15 13'Es sind keine Datensätze zum Anzei-
     A                                      gen vorhanden  '
     A                                      DSPATR(HI)
