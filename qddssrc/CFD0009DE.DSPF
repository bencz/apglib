     A*%%TS  SD  20091012  172134  APG         REL-V5R4M0  5722-WDS
     A*---------------------------------------------------------------*
     A* WINDOW ZUR SCHLÜSSELSELEKTION                                 *
     A*---------------------------------------------------------------*
     A*                                                               *
     A* AUTOR         :  A. PIEGER                                    *
     A*                                                               *
     A* ERSTELLT AM   :  02.09.2009                                   *
     A*                                                               *
     A* FUNKTION      :  SCHLÜSSELEINGABE                             *
     A*                                                               *
     A* ÄNDERUNGEN:                                                   *
     A* DATUM      VON   GRUND DER ÄNDERUNG                           *
     A*                                                               *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A  *DS3                                MSGLOC(24)
     A  *DS4                                MSGLOC(27)
     A                                      PRINT
     A                                      CA05(05)
     A                                      CA12(12)
     A*--------------------------------------------------------------
     A          R FMT02                     SFL
     A*%%TS  SD  20091012  132851  APG         REL-V5R4M0  5722-WDS
     A            HWPGM     R        H      REFFLD(HWPGM APG/PHINTXT)
     A            HWUSER    R        H      REFFLD(HWUSER APG/PHINTXT)
     A            HWLFPO    R        H      REFFLD(HWLFPO APG/PHINTXT)
     A            AUSW           1A  B  4  2VALUES(' ' '1')
     A  40                                  DSPATR(ND)
     A  40                                  DSPATR(PR)
     A            HWTEXT    R   41A  O  4  4REFFLD(HWTEXT APG/PHINTXT)
     A*--------------------------------------------------------------
     A          R FMT03                     SFLCTL(FMT02)
     A*%%TS  SD  20091009  100040  APG         REL-V5R4M0  5722-WDS
     A                                      CA13(13)
     A  *DS3                                SFLSIZ(0007)
     A  *DS4                                SFLSIZ(0007)
     A  *DS3                                SFLPAG(0006)
     A  *DS4                                SFLPAG(0006)
     A  *DS3                                WINDOW(FMT05)
     A  *DS4                                WINDOW(FMT05)
     A  03                                  CSRLOC(CURY       CURX)
     A                                      BLINK
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  96                                  SFLDSPCTL
     A  95                                  SFLCLR
     A  91                                  SFLEND
     A            SFRECNR        4S 0H      SFLRCDNBR(CURSOR)
     A* MESSAGEFELD FÜR DIE EIGENTLICHE NACHRICHT
     A  01        ERRMSG        78   M
     A* CURSORPOSITION BEI FEHLERAUSGABE
     A            CURX           3S 0H
     A            CURY           3S 0H
     A                                  1  2'CFD0009DE'
     A                                      COLOR(BLU)
     A            MAINUEB       32A  O  1 13DSPATR(HI)
     A                                  2  2'1=Als gelesen markieren'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R FMT04
     A*%%TS  SD  20091012  172134  APG         REL-V5R4M0  5722-WDS
     A                                      WINDOW(FMT05)
     A                                      OVERLAY
     A                                  7  6'Keine Daten angelegt'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------
     A          R FMT05
     A*%%TS  SD  20091012  171604  APG         REL-V5R4M0  5722-WDS
     A                                      WINDOW(12 30 11 46)
     A                                 10  2'F12=Abbrechen'
     A                                      COLOR(BLU)
     A                                 10 17'F13=Alle anzeigen'
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  19940426  153744  UB          REL-V2R2M0  5738-PW1
     A                                      ASSUME
     A                                 01 02' '
     A                                      DSPATR(ND)
